plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClassName = "duke.Main"
}

shadowJar {
    archiveBaseName = "duke"
    archiveClassifier = null
}

checkstyle {
    toolVersion = '8.29'
}

run{
    standardInput = System.in
}

/**
 * @@author akgrenSoar-reused
 * Solution adapted from: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.Exec.html
 */
task runtest(type: Exec) {
    workingDir "./text-ui-test"
    commandLine 'cmd', '/C', 'runtest.bat'
}

task myCheck {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'test' // JUnit test
    dependsOn 'runtest' // text-ui-test
}

/**
 * @@author akgrenSoar-reused
 * Solution adapted from: https://stackoverflow.com/a/45607934/6943913
 */
task myBuild(type: GradleBuild) {
    tasks = ['clean',
             'javadoc',
             'shadowJar'
    ]
}

